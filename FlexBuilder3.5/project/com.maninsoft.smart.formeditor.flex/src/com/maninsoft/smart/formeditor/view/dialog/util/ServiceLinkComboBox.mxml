<?xml version="1.0" encoding="utf-8"?>
<mx:HBox 
	width="100%" horizontalGap="2" verticalAlign="middle" horizontalScrollPolicy="off"
	xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:controls="com.maninsoft.smart.common.controls.*">
	<mx:Script>
		<![CDATA[
			import com.maninsoft.smart.formeditor.model.ServiceLink;
			import com.maninsoft.smart.formeditor.FormEditorBase;
			import com.maninsoft.smart.formeditor.assets.FormEditorAssets;
			import com.maninsoft.smart.formeditor.util.FormEditorPopup;
			import com.maninsoft.smart.formeditor.util.FormEditorInvoker;
			import com.maninsoft.smart.formeditor.util.FormEditorEvent;
			import com.maninsoft.smart.formeditor.model.FormLink;
			import com.maninsoft.smart.common.util.SmartUtil;
			import mx.collections.ArrayCollection;
			import com.maninsoft.smart.formeditor.model.FormDocument;
			import com.maninsoft.smart.formeditor.model.FormLinks;
			
			private var _form:FormDocument;
			[Bindable]
			private var options:ArrayCollection = new ArrayCollection();
			public var callback:Function;
			
			public function get form():FormDocument {
				return _form;
			}
			public function set form(form:FormDocument):void {
				this._form = form;
				refresh();
			}
			private function refresh():void {
				options.removeAll();
				options.addItem({label: "", value: null});
				if (form != null && form.serviceLinks != null && !SmartUtil.isEmpty(form.serviceLinks.serviceLinks)) {
					for each (var serviceLink:ServiceLink in form.serviceLinks.serviceLinks)
						options.addItem({label: serviceLink.name, value: serviceLink.id});
				}
			}
			
			private function popup(event:MouseEvent):void {
				var selectedItem:Object = comboBox.selectedItem;
				var serviceLink:ServiceLink = null;
				if (selectedItem != null && !SmartUtil.isEmpty(selectedItem.value) && 
					form != null && form.serviceLinks != null && !SmartUtil.isEmpty(form.serviceLinks.serviceLinks)) {
					var id:String = selectedItem.value as String;
					for each (var link:ServiceLink in form.serviceLinks.serviceLinks) {
						if (link.id != id)
							continue;
						serviceLink = link;
						break;
					}
				}
				FormEditorPopup.popupServiceLinkDialog(FormEditorBase.getInstance(), new Point(parentDocument.x, parentDocument.y), form, serviceLink, popupCallback);
			}
			private function popupCallback(event:FormEditorEvent):void {
				var serviceLink:ServiceLink = event.model as ServiceLink;
				FormEditorInvoker.setServiceLink(form, serviceLink);
				refresh();
				comboBox.selectItem(serviceLink.id);
				if (callback != null)
					callback(event);
			}
			
			public function selectItem(value:String):void {
				comboBox.selectItem(value);
			}
			public function get selectedItem():Object {
				return comboBox.selectedItem;
			}
			
			public function get text():String {
				return comboBox.text;
			}
			public function set text(text:String):void {
				comboBox.text = text;
			}
		]]>
	</mx:Script>
	<controls:SimpleComboBox id="comboBox" dataProvider="{options}"/>
	<mx:LinkButton icon="{FormEditorAssets.serviceLinkIcon}" toolTip="{resourceManager.getString('FormEditorETC', 'systemServiceConnectANEText')}" click="popup(event)" paddingLeft="0" paddingTop="0" paddingRight="0" paddingBottom="0"/>
</mx:HBox>
